#version 450
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : enable
#extension GL_GOOGLE_include_directive : enable

struct Cell {
    vec4 position;  
    vec3 vertPosition;
    vec3 normal;
    vec4 color;     
    ivec4 states;
} cell;

layout(std430, binding = 1) readonly buffer CellSSBOIn {Cell cellIn[ ]; };
layout(std430, binding = 2) buffer CellSSBOOut {Cell cellOut[ ]; };
layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;
layout(push_constant, std430) uniform pushConstant {
    uint64_t passedHours;
    float dayFraction;
};

#define UBO_LIGHT_NAME lightDirection
#include "ParameterUBO.glsl"
ivec2 gridXY = ubo.gridXY;

uint globalID_x = gl_GlobalInvocationID.x;
uint globalID_y = gl_GlobalInvocationID.y;
uint gridWidth = uint(max(gridXY.x, 1));
uint gridHeight = uint(max(gridXY.y, 1));
uint totalCells = gridWidth * gridHeight;
uint index = (globalID_y * gridWidth + (globalID_x % gridWidth) + (globalID_x / gridWidth) * gridHeight) % totalCells;

const vec4 blue       = vec4(0.0, 0.0, 1.0, 1.0);
const vec4 white      = vec4(1.0, 1.0, 1.0, 1.0);
const vec4 grey      = vec4(0.5, 0.5, 0.5, 1.0);

float hash21(vec2 p) {
    return fract(sin(dot(p, vec2(127.1, 311.7))) * 43758.5453);
}

float noise2(vec2 p) {
    vec2 i = floor(p);
    vec2 f = fract(p);
    float a = hash21(i);
    float b = hash21(i + vec2(1.0, 0.0));
    float c = hash21(i + vec2(0.0, 1.0));
    float d = hash21(i + vec2(1.0, 1.0));
    vec2 u = f * f * (3.0 - 2.0 * f);
    return mix(mix(a, b, u.x), mix(c, d, u.x), u.y);
}

float fbm(vec2 p) {
    float value = 0.0;
    float amplitude = 0.5;
    for (int i = 0; i < 5; ++i) {
        value += amplitude * noise2(p);
        p = p * 2.02 + vec2(11.5, 7.2);
        amplitude *= 0.5;
    }
    return value;
}

float ridged_fbm(vec2 p) {
    float value = 0.0;
    float amplitude = 0.5;
    for (int i = 0; i < 5; ++i) {
        float n = noise2(p);
        float ridge = 1.0 - abs(2.0 * n - 1.0);
        value += ridge * amplitude;
        p = p * 2.1 + vec2(9.2, 3.4);
        amplitude *= 0.5;
    }
    return value;
}

float terrain_height(vec2 p) {
    mat2 rot = mat2(0.866, -0.5, 0.5, 0.866);
    vec2 pr = rot * p;
    vec2 q = pr * 0.065;
    vec2 warp = vec2(
        fbm(q * 1.15 + vec2(4.0, 1.7)),
        fbm(q * 1.15 + vec2(7.2, 3.5)));
    q += warp * 0.75;

    float broad = fbm(q * 0.62) * 3.6;
    float base = fbm(q * 1.05) * 2.2;
    float ridge = ridged_fbm(q * 2.0) * 4.2;
    float crags = pow(max(ridged_fbm(q * 4.7), 0.0), 1.8) * 1.15;
    float macro = (sin(pr.x * 0.028) + sin(pr.y * 0.024)) * 0.85;
    float detail = fbm(q * 7.6) * 0.26;

    float mountainMask = smoothstep(0.52, 0.80, ridged_fbm(q * 0.95));
    float habitableLowlands = broad + base + macro;
    float mountainRelief = ridge + crags + detail;
    float lowlandBias = -0.55 * (1.0 - mountainMask);

    return habitableLowlands + mountainRelief * mountainMask + lowlandBias + 1.35;
}

const int alive         =  1;
const int dead          = -1;
float sizeAlive         = ubo.cellSize;
const float sizeDead    = 0.0f;

vec4 inPos      = cellIn[index].position;
vec4 inPosOn    = vec4( inPos.xyz, sizeAlive );
vec4 inPosOff   = vec4( inPos.xyz, sizeDead );
vec3 inVertPos  = cellIn[index].vertPosition;
vec3 inNormal   = cellIn[index].normal;
vec4 inColor    = cellIn[index].color;
ivec4 inStates  = cellIn[index].states;

const int cycleSize = 24;
ivec4 setState(int _alive, int targetIndex){ 
    int cycle = int(passedHours % cycleSize + 1); 
    return ivec4(_alive, targetIndex, cycle, passedHours); 
}

int getNeighbourIndex(ivec2 offset) {
    ivec2 indexPos = ivec2(index % gridXY.x, index / gridXY.x);
    ivec2 neighbourPos = indexPos + offset;
    if (neighbourPos.x < 0 || neighbourPos.y < 0 ||
        neighbourPos.x >= gridXY.x || neighbourPos.y >= gridXY.y) {
        return -1;
    }
    int neighbourIndex = neighbourPos.y * gridXY.x + neighbourPos.x;
    return neighbourIndex;
}

bool neighbourAlive(int index) {
    if (index < 0 || uint(index) >= totalCells) {
        return false;
    }
    int currentLife = cellIn[index].states.x;
    bool aliveState = currentLife == alive;
    return aliveState;
}

int cycleNeighbours(int range) {
    int neighboursAlive = 0;
    const int numOffsets = 8;
    ivec2 directNeighbourOffsets[numOffsets] = {
        ivec2(-1, -1), ivec2(-1, 0), ivec2(-1, 1), ivec2(0, -1),
        ivec2(0, 1), ivec2(1, -1), ivec2(1, 0), ivec2(1, 1)
    };
    for (int i = 0; i < numOffsets; i++) {
        ivec2 coordOffset = directNeighbourOffsets[i];
        int neighbourIndex = getNeighbourIndex(coordOffset);
        if (neighbourIndex < 0) {
            continue;
        }
        neighboursAlive += int(neighbourAlive(neighbourIndex));
    }
    return neighboursAlive;
}

int closestAliveNeighbourIndex() {
    const int numOffsets = 8;
    ivec2 directNeighbourOffsets[numOffsets] = {
        ivec2(-1, -1), ivec2(-1, 0), ivec2(-1, 1), ivec2(0, -1),
        ivec2(0, 1), ivec2(1, -1), ivec2(1, 0), ivec2(1, 1)
    };

    float nearestDist2 = 1e20;
    int nearestIndex = -1;
    for (int i = 0; i < numOffsets; i++) {
        int neighbourIndex = getNeighbourIndex(directNeighbourOffsets[i]);
        if (neighbourIndex < 0) {
            continue;
        }
        if (!neighbourAlive(neighbourIndex)) {
            continue;
        }

        vec2 delta = cellIn[neighbourIndex].position.xy - inPos.xy;
        float dist2 = dot(delta, delta);
        if (dist2 > 1e-6 && dist2 < nearestDist2) {
            nearestDist2 = dist2;
            nearestIndex = neighbourIndex;
        }
    }

    return nearestIndex;
}

vec2 gridBasePosition(uint cellIndex) {
    float startX = (float(gridXY.x) - 1.0) * -0.5;
    float startY = (float(gridXY.y) - 1.0) * -0.5;
    return vec2(startX + float(cellIndex % uint(gridXY.x)),
                startY + float(cellIndex / uint(gridXY.x)));
}

bool is_underwater_at(vec2 xy) {
    return terrain_height(xy) <= ubo.waterThreshold + ubo.waterRules.x;
}

bool is_shoreline_at(vec2 xy) {
    float h = terrain_height(xy) - ubo.waterThreshold;
    return h > ubo.waterRules.x && h <= ubo.waterRules.x + ubo.waterRules.y;
}

vec2 nearest_shore_position(vec2 startXY) {
    if (!is_underwater_at(startXY)) {
        return startXY;
    }

    vec2 gridStart = (vec2(gridXY) - vec2(1.0)) * -0.5;
    vec2 local = startXY - gridStart;
    ivec2 baseCell = ivec2(round(local));
    baseCell = clamp(baseCell, ivec2(0), gridXY - ivec2(1));

    float bestDist2 = 1e20;
    vec2 bestXY = startXY;

    const int maxRadius = 6;
    for (int radius = 1; radius <= maxRadius; ++radius) {
        for (int oy = -radius; oy <= radius; ++oy) {
            for (int ox = -radius; ox <= radius; ++ox) {
                if (abs(ox) != radius && abs(oy) != radius) {
                    continue;
                }

                ivec2 c = baseCell + ivec2(ox, oy);
                if (c.x < 0 || c.y < 0 || c.x >= gridXY.x || c.y >= gridXY.y) {
                    continue;
                }

                vec2 candidateXY = gridStart + vec2(c);
                if (!is_shoreline_at(candidateXY)) {
                    continue;
                }

                vec2 d = candidateXY - startXY;
                float dist2 = dot(d, d);
                if (dist2 < bestDist2) {
                    bestDist2 = dist2;
                    bestXY = candidateXY;
                }
            }
        }
        if (bestDist2 < 1e19) {
            break;
        }
    }

    if (bestDist2 < 1e19) {
        return bestXY;
    }

    // Fallback: if no shore found in local radius (large lakes/ocean),
    // search the full grid so cells never remain underwater when land exists.
    for (int y = 0; y < gridXY.y; ++y) {
        for (int x = 0; x < gridXY.x; ++x) {
            vec2 candidateXY = gridStart + vec2(float(x), float(y));
            if (!is_shoreline_at(candidateXY)) {
                continue;
            }

            vec2 d = candidateXY - startXY;
            float dist2 = dot(d, d);
            if (dist2 < bestDist2) {
                bestDist2 = dist2;
                bestXY = candidateXY;
            }
        }
    }

    // Final fallback: if no shoreline band exists nearby, use nearest dry land.
    if (bestDist2 >= 1e19) {
        for (int y = 0; y < gridXY.y; ++y) {
            for (int x = 0; x < gridXY.x; ++x) {
                vec2 candidateXY = gridStart + vec2(float(x), float(y));
                if (is_underwater_at(candidateXY)) {
                    continue;
                }

                vec2 d = candidateXY - startXY;
                float dist2 = dot(d, d);
                if (dist2 < bestDist2) {
                    bestDist2 = dist2;
                    bestXY = candidateXY;
                }
            }
        }
    }

    return bestXY;
}

vec4 moveTowardsTarget(vec4 sourcePos, int neighbourIndex, float cycleT) {
    if (neighbourIndex < 0) {
        return sourcePos;
    }

    vec2 baseXY = gridBasePosition(index);
    vec2 targetXY = gridBasePosition(uint(neighbourIndex));
    vec2 delta = targetXY - baseXY;
    float dist = length(delta);
    if (dist <= 1e-6) {
        sourcePos.xy = baseXY;
        return sourcePos;
    }

    // Complete full source->target travel across one full 24-hour cycle.
    sourcePos.xy = mix(baseXY, targetXY, cycleT);
    return sourcePos;
}

bool aliveCell          = inStates.x == alive;
bool deadCell           = inStates.x == dead;
float waterHeightOffset = 0.1;

bool live(int neighbours) { return (aliveCell && (neighbours == 3 || neighbours == 2)) || (deadCell && neighbours == 3); }
bool die(int neighbours)  { return (aliveCell && (neighbours < 2 || neighbours > 3)); }
vec4 colorIncrement     = inColor + vec4(float((passedHours % cycleSize + 1)) / float(cycleSize * 50), vec3(0.0));

void simulate(out Cell cell) {
    int neighbours  = cycleNeighbours(1);
    int cycleHour = int(passedHours % cycleSize) + 1;
    float cycleT = clamp(dayFraction, 0.0, 1.0);
    bool cycleStart = cycleHour == 1;
    bool cycleEnd = cycleHour == cycleSize;

    vec2 baseXY = gridBasePosition(index);
    vec2 shoreBaseXY = nearest_shore_position(baseXY);
    vec4 basePosOn = vec4(baseXY, inPos.z, sizeAlive);

    int storedTarget = inStates.y;
    int targetIndex = -1;
    if (aliveCell) {
        bool needsNewTarget = cycleStart || storedTarget < 0 || !neighbourAlive(storedTarget);
        targetIndex = needsNewTarget ? closestAliveNeighbourIndex() : storedTarget;
    }
    vec4 movedPosOn = moveTowardsTarget(basePosOn, targetIndex, cycleT);

    bool underWaterBase = is_underwater_at(baseXY);

    if (underWaterBase && aliveCell) {
       cell = Cell(inPosOff, inVertPos, inNormal, grey, setState(dead, -1) ); 
   } else if (!cycleEnd) {
        // During the day: no Conway birth/death transitions, only neighbour-follow motion.
        cell = aliveCell ? Cell(movedPosOn, inVertPos, inNormal, inColor, setState(alive, targetIndex))
                         : Cell(inPosOff, inVertPos, inNormal, grey, setState(dead, -1));
    } else {
        // End of cycle: apply Conway once, reset alive cells to base grid anchor.
        if (live(neighbours)) {
            if (underWaterBase) {
                bool shoreAvailable = !is_underwater_at(shoreBaseXY);
                vec4 bornPos = vec4(shoreBaseXY, inPos.z, sizeAlive);
                cell = shoreAvailable ? Cell(bornPos, inVertPos, inNormal, white, setState(alive, -1))
                                      : Cell(inPosOff, inVertPos, inNormal, grey, setState(dead, -1));
            } else {
                cell = Cell(basePosOn, inVertPos, inNormal, white, setState(alive, -1));
            }
        } else {
            cell = die(neighbours) ?    Cell(inPosOff, inVertPos, inNormal, grey, setState(dead, -1) ) :
                                        Cell(inPosOff, inVertPos, inNormal, inColor, setState(dead, -1) );
        }
    }
}

void main() {  
    if (cellIn[index].states.w == passedHours) { 
        cellOut[index] = cellIn[index]; 
        return; 
    } 
    simulate(cell);
    cellOut[index] = cell;
}


/*float findCenter(float a, float b){ return (a + b) * 0.5; }

float randomFloat(int value) {
    float random = fract(sin(float(value)) * 43758.5453); // Hash function
    return random;






    float getHeight(int target){ return cellIn[target].position.z; }

vec4 sidesHeight;
vec4 cornersHeight;
void setTileEdgeHeight(){
        cornersHeight.x = getHeight( getNeighbourIndex( ivec2(0, 0)) );
        cornersHeight.y = getHeight( getNeighbourIndex( ivec2(0, 1) )); 
        cornersHeight.z = getHeight( getNeighbourIndex( ivec2(-1, 1) )); 
        cornersHeight.w = getHeight( getNeighbourIndex( ivec2(-1, 0) ));
        sidesHeight.x = getHeight( getNeighbourIndex( ivec2(1, 0)  ));    
        sidesHeight.y = getHeight( getNeighbourIndex( ivec2(0, 1)  ));
        sidesHeight.z = getHeight( getNeighbourIndex( ivec2(-1, 0) ) ); 
        sidesHeight.w = getHeight( getNeighbourIndex( ivec2(0, -1) ) );
}
}*/